LEVELID LEVEL0

DEFALIAS
	$CONT_LEVER		5
	$PINV 			0
	$_B				14
	$_A				15
	$OPEN_GATE      1
END

ENTERING GAME
	IF TRUE
	THEN
		SET FALSE $PINV
		SET $CONT_LEVER = 7
		SET $OPEN_GATE = 0
	END
END

PRESS_FIRE AT SCREEN 0
	IF PLAYER_TOUCHES 4, 10
	THEN
		EXTERN 14
	END
END

PRESS_FIRE AT SCREEN 1
	IF PLAYER_TOUCHES 6, 10
	THEN
		EXTERN 15
	END
END

PRESS_FIRE AT SCREEN 2
	IF PLAYER_TOUCHES 3, 8
	THEN
		EXTERN 16
	END
END

ENTERING SCREEN 3
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_LEVER, 3, 8
	END
END

PRESS_FIRE AT SCREEN 3
	IF PLAYER_TOUCHES 1, 10
	THEN
		EXTERN 17
	END
END

ENTERING SCREEN 4
	IF $OPEN_GATE = 1
	THEN
		DECORATIONS
			7, 11, 11
			8, 11, 11
			9, 11, 11
		END
	END IF	
END

PRESS_FIRE AT SCREEN 4
	IF PLAYER_TOUCHES 5, 10
	THEN
		EXTERN 18
	END

	IF PLAYER_TOUCHES 11, 10
	IF $PINV = 7
	THEN
		SOUND 6
		DECORATIONS
			7, 11, 11
			8, 11, 11
			9, 11, 11
		END
		SET FALSE $PINV
		SET TRUE $OPEN_GATE
	END
END

ENTERING SCREEN 5
	IF TRUE
	THEN
		SET_FIRE_ZONE_TILES 0, 6, 0, 9
	END
END

PRESS_FIRE AT SCREEN 5
	IF PLAYER_TOUCHES 7, 9
	THEN
		EXTERN 22
	END

	IF PLAYER_IN_X_TILES 0,0
	THEN
		WIN GAME
	END
END

PRESS_FIRE AT SCREEN 7
	IF PLAYER_TOUCHES 11, 7
	THEN
		EXTERN 21
	END
END

ENTERING SCREEN 8
	IF TRUE
	THEN
		SET FALSE $_A
	END
END

PRESS_FIRE AT SCREEN 8
	IF PLAYER_TOUCHES 14, 7
	THEN
		EXTERN 20
	END

	IF PLAYER_TOUCHES 11, 9
	IF $_A = 0
	THEN
		DECORATIONS
			8, 3, 27
			8, 4, 27
		END
		SOUND 6
		SET TRUE $_A
	END
END

PRESS_FIRE AT SCREEN 9
	IF PLAYER_TOUCHES 10, 4
	THEN
		EXTERN 19
	END
END

END_OF_LEVEL

#------------------------------------------------------------------------------

LEVELID LEVEL1

DEFALIAS
	$PINV 			0
	$CONT_DISK_1	1
	$CONT_DISK_2 	2
	$CONT_DISK_3	3
	$OPEN_GATE_1	4
	$OPEN_GATE_2	5
	$OPEN_GATE_3	6
	$VALVED			7
	$SMALLBZWZ		8
	$SWITCH_1 		9
	$SWITCH_2 		10
	$SWITCH_3 		11
	$CONT_PANTSU 	13
	$_B				14
	$_A				15
	$GATE_1 		16
	$GATE_2 		17
END

ENTERING GAME
	IF TRUE
	THEN
		SET FALSE $PINV
		SET $CONT_DISK_1 = 10
		SET $CONT_DISK_2 = 10
		SET $CONT_DISK_3 = 10
		SET TRUE $CONT_PANTSU	# Save 1 bit although it doesn't make sense
		SET FALSE $OPEN_GATE_1
		SET FALSE $OPEN_GATE_2
		SET FALSE $OPEN_GATE_3
		SET FALSE $VALVED
		SET FALSE $SMALLBZWZ
		SET FALSE $SWITCH_1
		SET FALSE $SWITCH_2
		SET FALSE $SWITCH_3
		SET FASE $GATE_1
		SET TRUE $GATE_2

		# debuf
		#SET TRUE $PINV0
		#WARP_TO 21, 7, 1
	END
END

PRESS_FIRE AT SCREEN 1
	IF PLAYER_TOUCHES 2, 8
	THEN
		EXTERN 23
	END
END

ENTERING SCREEN 9
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_DISK_1, 7, 7
	END
END

PRESS_FIRE AT SCREEN 10
	IF PLAYER_TOUCHES 7, 10
	THEN
		EXTERN 25
	END

	IF PLAYER_TOUCHES 4, 9
	IF $PINV = 10
	IF $OPEN_GATE_1 = 0
	THEN
		SET TRUE $OPEN_GATE_1
		SET FALSE $PINV
		EXTERN 26
		SOUND 6
	END
END

ENTERING SCREEN 11
	IF $OPEN_GATE_1 = 1
	THEN
		DECORATIONS
			3, 11, 11
			4, 11, 11
			5, 11, 11
			6, 11, 0
			7, 11, 0
			8, 11, 0
			9, 11, 10
			10, 11, 0
			11, 11, 0
			12, 11, 0
		END
	END
END

PRESS_FIRE AT SCREEN 11
	IF PLAYER_TOUCHES 2, 10
	THEN
		EXTERN 24
	END
END

PRESS_FIRE AT SCREEN 15
	IF PLAYER_TOUCHES 7, 10
	THEN
		EXTERN 25
	END

	IF PLAYER_TOUCHES 4, 9
	IF $PINV = 10
	IF $OPEN_GATE_2 = 0
	THEN
		SET TRUE $OPEN_GATE_2
		SET FALSE $PINV
		EXTERN 26
		SOUND 6
	END
END

ENTERING SCREEN 16
	IF $OPEN_GATE_2 = 1
	THEN
		DECORATIONS
			3, 11, 0
			4, 11, 2
			5, 11, 1
			6, 11, 0
			7, 11, 0
			8, 11, 0
			9, 11, 0
			10, 11, 1
			11, 11, 3
			12, 11, 0
		END
	END
END

ENTERING SCREEN 18 
	IF $SMALLBZWZ = 1
	THEN
		SET TILE (15, 10) = 0
	END
END

PRESS_FIRE AT SCREEN 18
	IF PLAYER_TOUCHES 2, 10
	IF $SMALLBZWZ = 0
	THEN
		SET TRUE $SMALLBZWZ
		SOUND 6
		SET TILE (15, 10) = 0
	END
END

ENTERING SCREEN 19
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_DISK_2, 14, 5
	END

	IF $VALVED = 1
	THEN
		DECORATIONS
			5, 4, 8
			6, 4, 8
			7, 4, 8
			8, 4, 8
			5, 5, 0
			6, 5, 0
			7, 5, 0
			8, 5, 0
			5, 6, 11
			6, 6, 11
			7, 6, 11
			8, 6, 11
		END
	END
END

PRESS_FIRE AT SCREEN 20
	IF PLAYER_TOUCHES 7, 10
	THEN
		EXTERN 25
	END

	IF PLAYER_TOUCHES 4, 9
	IF $PINV = 10
	IF $OPEN_GATE_3 = 0
	THEN
		SET TRUE $OPEN_GATE_3
		SET FALSE $PINV
		EXTERN 26
		SOUND 6
	END
END

ENTERING SCREEN 21
	IF $OPEN_GATE_3 = 1
	THEN
		DECORATIONS
			3, 11, 10
			4, 11, 0
			5, 11, 10
			6, 11, 0
			7, 11, 10
			8, 11, 0
			9, 11, 10
			10, 11, 0
			11, 11, 10
			12, 11, 0
		END
	END
END

PRESS_FIRE AT SCREEN 21
	IF PLAYER_TOUCHES 6, 1
	THEN
		RECHARGE
		SOUND 0
	END
END

PRESS_FIRE AT SCREEN 22
	# Messages
	IF PLAYER_TOUCHES 4, 4
	THEN
		EXTERN 29
		BREAK
	END

	IF PLAYER_TOUCHES 7, 4
	THEN
		EXTERN 30
		BREAK
	END

	IF PLAYER_TOUCHES 10, 4
	THEN
		EXTERN 31
		BREAK
	END

	IF PLAYER_TOUCHES 3, 4
	THEN
		FLIPFLOP $GATE_1
		FLIPFLOP $GATE_2
		EXTERN 27
	END

	IF PLAYER_TOUCHES 6, 4
	THEN
		FLIPFLOP $GATE_1
		EXTERN 27
	END

	IF PLAYER_TOUCHES 9, 4
	THEN
		SET FASE $GATE_1
		SET TRUE $GATE_2
		EXTERN 27
	END
END

ENTERING SCREEN 23
	# Clear blocks to disk
	IF $SWITCH_1 = 1
	THEN
		SET TILE (5, 6) = 0
	END

	IF $SWITCH_2 = 1
	THEN
		SET TILE (5, 5) = 0
	END

	IF $SWITCH_3 = 1
	THEN
		SET TILE (5, 4) = 0
	END

	IF TRUE
	THEN
		ADD_CONTAINER $CONT_DISK_3, 12, 3
	END
END

PRESS_FIRE AT SCREEN 23
	IF PLAYER_TOUCHES 13, 9
	THEN
		RECHARGE
		SOUND 0
	END
END

PRESS_FIRE AT SCREEN 24
	IF PLAYER_TOUCHES 11, 9
	IF $VALVED = 0
	THEN
		SET TRUE $VALVED
		SOUND 6
		EXTERN 27
	END

	IF PLAYER_TOUCHES 5, 3
	THEN
		EXTERN 28
	END
END

ENTERING SCREEN 27
	# Open gates depending on vars
	IF $GATE_1 = 1
	THEN
		DECORATIONS
			4, 1, 10
			4, 2, 10
			4, 9, 25
			4, 10, 25
		END
	END

	IF $GATE_2 = 1
	THEN
		DECORATIONS
			6, 1, 10
			6, 2, 10
			6, 9, 25
			6, 10, 25
		END
	END

	# Delte switches depending on vars
	IF $SWITCH_1 = 1
	THEN
		SET TILE (2, 3) = 27
	END

	IF $SWITCH_2 = 1
	THEN
		SET TILE (2, 7) = 27
	END

	IF $SWITCH_3 = 1
	THEN
		SET TILE (2, 11) = 27
	END
END

PRESS_FIRE AT SCREEN 27
	# Switches
	IF PLAYER_TOUCHES 2, 2
	IF $SWITCH_1 = 0
	THEN
		SET TRUE $SWITCH_1
		SET TILE (2, 3) = 27
		EXTERN 27		
		SOUND 6
	END

	IF PLAYER_TOUCHES 2, 6
	IF $SWITCH_2 = 0
	THEN
		SET TRUE $SWITCH_2
		SET TILE (2, 7) = 27
		EXTERN 27		
		SOUND 6
	END

	IF PLAYER_TOUCHES 2, 10
	IF $SWITCH_3 = 0
	THEN
		SET TRUE $SWITCH_3
		SET TILE (2, 11) = 27
		EXTERN 27		
		SOUND 6
	END
END

ENTERING SCREEN 26
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_PANTSU, 7, 7
	END
END

PRESS_FIRE AT SCREEN 26
	IF $PINV = 1
	THEN
		WIN GAME
	END
END

END_OF_LEVEL

#------------------------------------------------------------------------------

LEVELID LEVEL2

DEFALIAS
	$PINV 			0
	$CONT_ENGINE_1	1
	$CONT_ENGINE_2	2
	$CONT_ENGINE_3	3
	$CONT_WEIGHT	4
	$CONT_LEVER		5
	$CONT_KEY		6
	$CONT_DOOR_1    7
	$CONT_DOOR_2    8
	$CONT_DOOR_3    9
	$CONT_LOCK		10
	$DOOR_IS_OPEN	11
	$_D				12
	$_C				13
	$_B				14
	$_A				15
	$CONT_BTN_1     16
	$CONT_BTN_2     17
	$HAS_GOT		30
	$LAME_BOSS_ON	31
END

ENTERING GAME
	IF TRUE
	THEN
		SET FALSE $PINV
		SET $CONT_ENGINE_1	= 3
		SET $CONT_ENGINE_2	= 4
		SET $CONT_ENGINE_3	= 5
		SET $CONT_WEIGHT	= 6
		SET $CONT_LEVER		= 7
		SET $CONT_KEY 		= 8
		SET FALSE $CONT_DOOR_1
		SET FALSE $CONT_DOOR_2
		SET FALSE $CONT_DOOR_3
		SET FALSE $CONT_LOCK
		SET FALSE $CONT_BTN_1
		SET FALSE $CONT_BTN_2
		SET FALSE $LAME_BOSS_ON

		# SET $PINV = 8
		# SET TRUE $DOOR_IS_OPEN
		# WARP_TO 28,2,2

	END IF
END

# Order puzzle
ENTERING SCREEN 2
	IF TRUE
	THEN
		SET FALSE $_A
	END
END

PRESS_FIRE AT SCREEN 2
	IF PLAYER_TOUCHES 10, 5
	IF $_A = 0
	THEN
		SOUND 0
		SET TRUE $_A
		BREAK
	END

	IF PLAYER_TOUCHES 7, 5
	IF $_A = 1
	THEN
		SOUND 0
		SET $_A = 2
		BREAK
	END

	IF PLAYER_TOUCHES 4, 5
	IF $_A = 2
	THEN
		SOUND 0
		SET $_A = 3
	END

	IF $_A != 3
	THEN
		DECORATIONS
			4, 6, 13
			7, 6, 13
			10, 6, 13
		END
		SOUND 2
		BREAK
	END

	IF TRUE
	THEN
		SET TILE (14, 10) = 0
		SOUND 0
	END
END

# Third piece
ENTERING SCREEN 3
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_ENGINE_3, 12, 9
	END IF
END

PRESS_FIRE AT SCREEN 3
	IF PLAYER_TOUCHES 8, 10
	THEN
		EXTERN 7
	END
END

# First piece
ENTERING SCREEN 4
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_ENGINE_1, 2, 9
	END IF
END

PRESS_FIRE AT SCREEN 4
	IF PLAYER_TOUCHES 7, 10
	THEN
		EXTERN 7
	END
END

# Rotary columns puzzle
ENTERING SCREEN 5
	IF TRUE
	THEN
		SET $_A = 2
		SET $_B = 4
		SET $_C = 3
	END
END

PRESS_FIRE AT SCREEN 5
	# Column A moves 1 + 3 UP
	IF PLAYER_TOUCHES 5, 9
	THEN
		SET TILE (5, #$_A) = 13
		DECRANGE $_A, 1, 5
		SET TILE (5, #$_A) = 0
		SET TILE (9, #$_C) = 13
		DECRANGE $_C, 1, 5
		SET TILE (9, #$_C) = 0
		SOUND 6
	END

	# Column A moves 2 + 3 DOWN
	IF PLAYER_TOUCHES 7, 9
	THEN
		SET TILE (7, #$_B) = 13
		INCRANGE $_B, 1, 5
		SET TILE (7, #$_B) = 0
		SET TILE (9, #$_C) = 13
		INCRANGE $_C, 1, 5
		SET TILE (9, #$_C) = 0
		SOUND 6
	END

	# Column C moves 1 + 2 UP
	IF PLAYER_TOUCHES 9, 9
	THEN
		SET TILE (5, #$_A) = 13
		DECRANGE $_A, 1, 5
		SET TILE (5, #$_A) = 0
		SET TILE (7, #$_B) = 13
		DECRANGE $_B, 1, 5
		SET TILE (7, #$_B) = 0
		SOUND 6
	END
END

PRESS_FIRE AT SCREEN 6
	IF PLAYER_TOUCHES 2, 5
	THEN
		EXTERN 12
	END
END

PRESS_FIRE AT SCREEN 7
	IF PLAYER_TOUCHES 11, 10
	THEN
		EXTERN 8
	END
END

# Engine piece #2

ENTERING SCREEN 8
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_ENGINE_2, 2, 9
	END
END

PRESS_FIRE AT SCREEN 8
	IF PLAYER_TOUCHES 6, 9
	THEN
		EXTERN 7
	END
END

# Two skulls and a heart

ENTERING SCREEN 9
	IF TRUE
	THEN
		# Other stuff
		SET FALSE $_A
		SET FALSE $_C
		SET_FIRE_ZONE_TILES 6, 4, 9, 6
	END
END

PRESS_FIRE AT SCREEN 9
	IF PLAYER_TOUCHES 12, 6
	THEN
		EXTERN 2
	END

	IF PLAYER_TOUCHES 13, 10
	IF $_A = 0
	THEN
		SET TRUE $_A
		DECORATIONS
			1, 5, 0
			1, 6, 0
		END
		SOUND 6
		BREAK
	END

	# You will only trigger this zone if you are jumping!
	IF PLAYER_IN_X_TILES 6, 6
	IF PLAYER_IN_Y_TILES 4, 5
	IF $_C = 0
	THEN
		SET TRUE $_C
		DECORATIONS
			6, 4, 19
			6, 5, 19
			6, 6, 19
		END
		SOUND 6
		SET_FIRE_ZONE_TILES 3, 4, 3, 6
		BREAK
	END

END

# Be quick or be dead!

ENTERING SCREEN 10
	IF TRUE
	THEN
		SET FALSE $_A
	END
END

PRESS_FIRE AT SCREEN 10
	IF PLAYER_TOUCHES 10, 2
	IF $_A = 0
	THEN
		$_A = 1
		DECORATIONS
			1, 2, 0
			1, 3, 0
		END
		SOUND 6
		BREAK
	END
END

# Screen with key and trap
ENTERING SCREEN 14
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_KEY, 13, 6
		SET FALSE $_A
	END
END

PRESS_FIRE AT SCREEN 14
	IF PLAYER_TOUCHES 4, 2
	IF $_A = 0
	THEN
		DECORATIONS
			6, 4, 16
			6, 5, 14
			4, 2, 53
		END
		SET TRUE $_A
		SOUND 6
		BREAK
	END

	IF PLAYER_TOUCHES 4, 2
	IF $_A = 1
	THEN
		DECORATIONS
			6, 4, 0
			6, 5, 0
			4, 2, 30
		END
		SET FALSE $_A
		SOUND 6
		BREAK
	END
END

# Screen with big compuerta
ENTERING SCREEN 15
	IF TRUE
	THEN
		SET FALSE $_A
	END
END

PRESS_FIRE AT SCREEN 15
	IF PLAYER_TOUCHES 8, 10
	IF $_A = 0
	THEN
		DECORATIONS
			12, 2, 13
			12, 3, 9
			12, 4, 13
			12, 5, 9
			12, 6, 13
			12, 7, 0
			8, 10, 53
		END
		SET TRUE $_A
		SOUND 6
		BREAK
	END

	IF PLAYER_TOUCHES 8, 10
	IF $_A = 1
	THEN
		DECORATIONS
			12, 2, 9
			12, 3, 13
			12, 4, 9
			12, 5, 13
			12, 6, 9
			12, 7, 13
			8, 10, 30
		END
		SET FALSE $_A
		SOUND 6
	END
END

# Screen with switch to trap door and trap for baddies
# Switches are one way here, sorry.

ENTERING SCREEN 16
	IF TRUE
	THEN
		SET FLAG $_A = 0
		SET FLAG $_B = 0
	END
END

PRESS_FIRE AT SCREEN 16
	IF PLAYER_TOUCHES 9, 2
	IF $_A = 0
	THEN
		DECORATIONS
			9, 2, 53
			3, 0, 0
			4, 0, 0
			5, 0, 0
		END
		SOUND 6
		$_A = 1
		BREAK
	END

	IF PLAYER_TOUCHES 3, 10
	IF $_B = 0
	THEN
		DECORATIONS
			3, 10, 53
			5, 10, 0
			6, 10, 0
			7, 10, 0
			8, 10, 0
			9, 10, 0
			10, 10, 0
			11, 10, 0
		END
		SOUND 6
		PAUSE 25
		DECORATIONS
			5, 9, 19
			5, 10, 19
		END
		SOUND 6
		PAUSE 10
		DECORATIONS
			7, 9, 19
			7, 10, 19
		END
		SOUND 6
		PAUSE 10
		DECORATIONS
			9, 9, 19
			9, 10, 19
		END
		SOUND 6
		PAUSE 10
		DECORATIONS
			2, 6, 0
			2, 7, 0
		END
		SOUND 6
		$_B = 1
	END
END

# Trap for baddies
ENTERING SCREEN 17
	IF TRUE
	THEN
		SET FALSE $_A
	END
END

PRESS_FIRE AT SCREEN 17
	IF PLAYER_TOUCHES 1, 10
	IF $_A = 1
	THEN
		EXTERN 3
		BREAK
	END

	IF PLAYER_TOUCHES 1, 10
	IF $PINV = 7
	THEN
		# Do trap
		DECORATIONS
			1, 5, 0
			2, 5, 0
			3, 5, 0
			4, 5, 0
			5, 5, 0
			6, 5, 0
			7, 5, 0
			8, 5, 0
			9, 5, 0
			10, 5, 0
			11, 5, 0
		END
		SET TRUE $_A
		SOUND 6
		# And the lever disappears forever!
		SET FALSE $PINV
		BREAK
	END

	IF PLAYER_TOUCHES 1, 10
	THEN
		EXTERN 4
	END
END

ENTERING SCREEN 19
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_LEVER, 13, 3
		SET FALSE $_A
	END
END

PRESS_FIRE AT SCREEN 19
	IF $_A = 0
	THEN
		SET TRUE $_A
		DECORATIONS
			8, 6, 0
			9, 6, 0
			8, 7, 13
			9, 7, 27
			1, 10, 53
		END
		SOUND 6
	END
END

ENTERING SCREEN 22
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_WEIGHT, 9, 8
	END
END

PRESS_FIRE AT SCREEN 24
	IF PLAYER_TOUCHES 3, 10
	THEN
		EXTERN 1
	END

	IF PLAYER_TOUCHES 1, 10
	THEN
		RECHARGE
		SOUND 0
	END
END

# Door screen. Door can be open if
# $CONT_DOOR_1 = 3
# $CONT_DOOR_2 = 4
# $CONT_DOOR_3 = 5
# And
# (FIRE @ (4, 3) AND $CONT_BTN_2 = 6) OR
# (FIRE @ (B, 3) AND 4CONT_BTN_1 = 6)

ENTERING SCREEN 27
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_DOOR_1, 6, 9
		ADD_CONTAINER $CONT_DOOR_2, 7, 9
		ADD_CONTAINER $CONT_DOOR_3, 8, 9
		ADD_CONTAINER $CONT_BTN_1, 4, 3
		ADD_CONTAINER $CONT_BTN_2, 11, 3
	END

	IF $DOOR_IS_OPEN = 1
	THEN
		DECORATIONS
			15, 8, 0
			15, 9, 0
		END
	END
END

PRESS_FIRE AT SCREEN 27
	IF PLAYER_TOUCHES 2, 3
	THEN
		EXTERN 5
		BREAK
	END

	IF PLAYER_TOUCHES 3, 9
	THEN
		EXTERN 6
		BREAK
	END

	IF $DOOR_IS_OPEN = 1
	THEN
		BREAK
	END

	IF TRUE
	THEN
		SET FLAG $_A = 0
		SET FLAG $_B = 0
	END

	# prepare some stuff

	IF $CONT_DOOR_1 = 3
	IF $CONT_DOOR_2 = 4
	IF $CONT_DOOR_3 = 5
	THEN
		SET FLAG $_A = 1
	END

	IF PLAYER_TOUCHES 4, 3
	IF $CONT_BTN_2 != 6
	THEN
		EXTERN 9
		BREAK
	END

	IF PLAYER_TOUCHES 11, 3
	IF $CONT_BTN_1 != 6
	THEN
		EXTERN 9
		BREAK
	END
	
	IF PLAYER_TOUCHES 4, 3
	# ASSUMED: $CONT_BTN_2 = 6
	IF $_A = 0
	THEN
		EXTERN 10
		EXTERN 11
		BREAK
	END

	IF PLAYER_TOUCHES 11, 3
	# ASSUMED $CONT_BTN_1 = 6
	IF $_A = 0
	THEN
		EXTERN 10
		EXTERN 11
		BREAK
	END

	IF PLAYER_TOUCHES 4, 3
	# ASSUMED: $CONT_BTN_2 = 6
	# ASSUMED: $_A = 1
	THEN
		SET FLAG $_B = 1
	END

	IF PLAYER_TOUCHES 11, 3
	# ASSUMED: $CONT_BTN_1 = 6
	$ ASSUMED: $_A = 1
	THEN
		SET FLAG $_B = 1
	END

	IF $_B = 1
	THEN
		SET TRUE $DOOR_IS_OPEN
		DECORATIONS
			15, 8, 0
			15, 9, 0
		END
		SOUND 6
	END
END

ENTERING SCREEN 28
	IF TRUE
	THEN
		ADD_CONTAINER $CONT_LOCK, 13, 10
	END

	IF $CONT_LOCK = 8
	THEN
		DECORATIONS
			14, 9, 0
			14, 10, 0
		END
	END
END

PRESS_FIRE ON SCREEN 28
	IF $CONT_LOCK = 8
	IF PLAYER_TOUCHES 13, 10
	THEN
		DECORATIONS
			14, 9, 0
			14, 10, 0
		END
		SOUND 6
	END

	IF PLAYER_TOUCHES 14, 6
	THEN
		RECHARGE
		SOUND 0
	END
END

ENTERING SCREEN 29
	IF TRUE
	THEN
		SET FALSE $LAME_BOSS_ON
		SET_FIRE_ZONE_TILES 2,0,2,11
		# Stop the music
		EXTERN 127
		SET $_A = 0
	END
END

PRESS_FIRE AT SCREEN 29
	IF PLAYER_IN_X_TILES 2, 2
	THEN
		DECORATIONS
			0, 9, 13
			0, 10, 13
		END
		SOUND 2
		SET_FIRE_ZONE_TILES 7,9,8,9
	END

	IF PLAYER_IN_X_TILES 7,8
	IF $_A = 0
	THEN
		SOUND 6
		SET TRUE $LAME_BOSS_ON
		# Play the boss music
		EXTERN 126
		SET $_A = 1
	END
END IF

END_OF_LEVEL